# æ·±å…¥è§£æ EigenLayer åº•å±‚è®¾è®¡åŸç†å’Œæºç 

# ä¸€. EigenLayer çš„ä»£ç æ¶æ„

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtp5-DbUAA5bgG.jpg)

# äºŒ. EigenLayer çš„åŠŸèƒ½æ¨¡å—

## 1.æ³¨å†Œæˆä¸º Operator

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtqeykaIAAM-Hi.png)

ğŸ’¡Operator è°ƒç”¨ registerAsOperator çš„æ–¹æ³•ç§°ä¸ºèŠ‚ç‚¹è¿è¥å•†
ğŸ’¡ğŸ’¡å°† operator è‡ªå·±ç»‘å®šæˆä¸ºäº† staker
ğŸ’¡ğŸ’¡å°† operator  è‡ªè´¨æŠ¼çš„ä»½é¢ delegate ç»™è‡ªå·±

## 2.Staking æµç¨‹

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtrAaHbQAAx4Hk.jpg)

æµç¨‹ä¸€ï¼š ETH è´¨æŠ¼

- Staker è°ƒç”¨ EigenPodManager åˆ›å»ºä¸€ä¸ª Pod, å¯ä»¥ä¸åšï¼Œè´¨æŠ¼çš„æ—¶å€™ä¼šå»åˆ¤æ–­æ˜¯å¦å·²ç»åˆ›å»ºäº† pod
- Staker è°ƒç”¨ stake æ–¹æ³•æŠŠé’±æ‰“å…¥åˆ°å¯¹åº”çš„ Pod é‡Œé¢å»
- Staker è°ƒç”¨ verifyWithdrawalCredentials éªŒè¯ä¿¡æ ‡é“¾çš„çŠ¶æ€è·Ÿï¼Œé€šè¿‡ EIP-4788 æ¥è·å–ä¿¡æ ‡é“¾æœ€æ–°çš„åŒºå—æŠ•
- è‹¥ stake ä»¥å‰ Delegate è¿‡ï¼Œç›´æ¥æŠŠè´¨æŠ¼äº§ç”Ÿçš„ shares åŠ ç»™å¯¹åº”çš„ Operator, è‹¥æ²¡æœ‰ delegate è¿‡ï¼Œç›´æ¥è°ƒç”¨ delegateTo å°†è´¨æŠ¼ä»½é¢ Delegate ç»™å¯¹åº”çš„ operator



æµç¨‹äºŒï¼šERC20 Token è´¨æŠ¼(stETH, mETH, swETH, dETH(DappLink deth))

- Staker è°ƒç”¨ depositIntoStrategy å°† ERC20 Token è´¨æŠ¼åˆ°å¯¹åº”ä»£å¸ç­–ç•¥
- è‹¥ stake ä»¥å‰ Delegate è¿‡ï¼Œç›´æ¥æŠŠè´¨æŠ¼äº§ç”Ÿçš„ shares åŠ ç»™å¯¹åº”çš„ Operator, è‹¥æ²¡æœ‰ delegate è¿‡ï¼Œç›´æ¥è°ƒç”¨ delegateTo å°†è´¨æŠ¼ä»½é¢ Delegate ç»™å¯¹åº”çš„ operator

## 3.Delegate æµç¨‹

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtrIlKbAAAMmCb.jpg)

## 4.æ’é˜Ÿå–æ¬¾

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtrMpIaYAAJcwF.jpg)





## 5.å®Œæˆæ’é˜Ÿ

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtrQv9a8AAJbx0.jpg)
  

## 6. Staker ä»ä¿¡æ ‡é“¾ï¼ˆEigenLayerï¼‰



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtrU2lbsAA_XKn.jpg)



# ä¸‰.EigenLayer çš„æºç è§£æ

## 1.æ³¨å†Œæˆä¸º Operator æºç è§£æ

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtrgwwaIAEXUpR.png)

```
function registerAsOperator(
    OperatorDetails calldata registeringOperatorDetails,
    string calldata metadataURI
) external {
    require(
        _operatorDetails[msg.sender].earningsReceiver == address(0),
        "DelegationManager.registerAsOperator: operator has already registered"
    );
    _setOperatorDetails(msg.sender, registeringOperatorDetails);
    SignatureWithExpiry memory emptySignatureAndExpiry;
    // delegate from the operator to themselves
    _delegate(msg.sender, msg.sender, emptySignatureAndExpiry, bytes32(0));
    // emit events
    emit OperatorRegistered(msg.sender, registeringOperatorDetails);
    emit OperatorMetadataURIUpdated(msg.sender, metadataURI);
}
```

- Operator è°ƒç”¨ registerAsOperator ä¿¡æ¯ï¼Œä¼ å…¥åŒ…å« OperatorDetails å’Œ metadataURI æ•°æ®
- è°ƒç”¨ _delegate æ–¹æ³•æŠŠè‡ªå·±å’Œè‡ªå·±ï¼Œå³è‡ªå·±å³æ˜¯ Operator, ä¹Ÿæ˜¯ staker
- æŠ›å‡º OperatorRegistered å’Œ OperatorMetadataURIUpdated äº‹ä»¶

```
function _delegate(
    address staker,
    address operator,
    SignatureWithExpiry memory approverSignatureAndExpiry,
    bytes32 approverSalt
) internal onlyWhenNotPaused(PAUSED_NEW_DELEGATION) {
    require(!isDelegated(staker), "DelegationManager._delegate: staker is already actively delegated");
    require(isOperator(operator), "DelegationManager._delegate: operator is not registered in EigenLayer");

    address _delegationApprover = _operatorDetails[operator].delegationApprover;
  
    if (_delegationApprover != address(0) && msg.sender != _delegationApprover && msg.sender != operator) {
       
        require(
            approverSignatureAndExpiry.expiry >= block.timestamp,
            "DelegationManager._delegate: approver signature expired"
        );

        require(
            !delegationApproverSaltIsSpent[_delegationApprover][approverSalt],
            "DelegationManager._delegate: approverSalt already spent"
        );
        delegationApproverSaltIsSpent[_delegationApprover][approverSalt] = true;


        bytes32 approverDigestHash = calculateDelegationApprovalDigestHash(
            staker,
            operator,
            _delegationApprover,
            approverSalt,
            approverSignatureAndExpiry.expiry
        );


        EIP1271SignatureUtils.checkSignature_EIP1271(
            _delegationApprover,
            approverDigestHash,
            approverSignatureAndExpiry.signature
        );
    }

    delegatedTo[staker] = operator;
    emit StakerDelegated(staker, operator);

    (IStrategy[] memory strategies, uint256[] memory shares)
        = getDelegatableShares(staker);

    for (uint256 i = 0; i < strategies.length;) {
        _increaseOperatorShares({
            operator: operator,
            staker: staker,
            strategy: strategies[i],
            shares: shares[i]
        });

        unchecked { ++i; }
    }
}
```

- delegatedTo[staker] = operator è¿™å¥ä»£ç æ˜¯å°† Operator å’Œ staker è¿›è¡Œç»‘å®šï¼Œå½“ operator è°ƒç”¨çš„æ—¶å€™ï¼Œ staker å’Œ operator éƒ½æ˜¯åŒä¸€ä¸ªï¼Œæ‰€ä»¥æˆ‘ä»¬è¯´ operator æ—¢æ˜¯ Operator, ä¹Ÿæ˜¯ staker;
- è°ƒç”¨ getDelegatableShares æ–¹æ³•ï¼Œè·å–åˆ° staker åœ¨å¯¹åº”ç­–ç•¥é‡Œé¢è´¨æŠ¼ä»½é¢ï¼Œé€šè¿‡ _increaseOperatorShares æŠŠå¯¹åº”çš„è´¨æŠ¼ shares åŠ ç»™äº† operator;

```
function getDelegatableShares(address staker) public view returns (IStrategy[] memory, uint256[] memory) {
    // Get currently active shares and strategies for `staker`
    int256 podShares = eigenPodManager.podOwnerShares(staker);
    (IStrategy[] memory strategyManagerStrats, uint256[] memory strategyManagerShares) 
        = strategyManager.getDeposits(staker);

    // Has no shares in EigenPodManager, but potentially some in StrategyManager
    if (podShares <= 0) {
        return (strategyManagerStrats, strategyManagerShares);
    }

    IStrategy[] memory strategies;
    uint256[] memory shares;

    if (strategyManagerStrats.length == 0) {
        // Has shares in EigenPodManager, but not in StrategyManager
        strategies = new IStrategy[](1);
        shares = new uint256[](1);
        strategies[0] = beaconChainETHStrategy;
        shares[0] = uint256(podShares);
    } else {
        // Has shares in both
        
        // 1. Allocate return arrays
        strategies = new IStrategy[](strategyManagerStrats.length + 1);
        shares = new uint256[](strategies.length);
        
        // 2. Place StrategyManager strats/shares in return arrays
        for (uint256 i = 0; i < strategyManagerStrats.length; ) {
            strategies[i] = strategyManagerStrats[i];
            shares[i] = strategyManagerShares[i];

            unchecked { ++i; }
        }

        // 3. Place EigenPodManager strat/shares in return arrays
        strategies[strategies.length - 1] = beaconChainETHStrategy;
        shares[strategies.length - 1] = uint256(podShares);
    }

    return (strategies, shares);
}
```

- å¦‚æœä½ åœ¨ EigenPod çš„ç­–ç•¥é‡Œé¢æ²¡æœ‰è´¨æŠ¼ï¼Œé‚£ç›´æ¥è¿”å›å¯¹åº”çš„é eigenPod ç­–ç•¥çš„ strategyManagerStrats å’Œ strategyManagerShares
- å¦‚æœä½ åœ¨  EigenPod çš„ç­–ç•¥é‡Œé¢æœ‰è´¨æŠ¼ è‹¥åœ¨é eigenPod ç­–ç•¥æ²¡æœ‰è´¨æŠ¼ï¼Œå°† beaconChainETHStrategy ç­–ç•¥åŠå…¶ podShares åšä¸º strategies å’Œ shares æ•°ç»„çš„ç¬¬ä¸€é¡¹ï¼Œç›´æ¥è¿”å›å» è‹¥åœ¨é eigenPod ç­–ç•¥æœ‰è´¨æŠ¼ï¼ŒæŠŠ beaconChainETHStrategy å’Œå…¶ share åšä¸ºæ•°ç»„çš„æœ€åä¸€é¡¹è¿”å›å»

```
function _increaseOperatorShares(address operator, address staker, IStrategy strategy, uint256 shares) internal {
    operatorShares[operator][strategy] += shares;
    emit OperatorSharesIncreased(operator, staker, strategy, shares);
}
```

- operatorShares[operator][strategy] += shares; ç›´æ¥æŠŠç­–ç•¥é‡Œé¢å¯¹åº”çš„ shares åŠ ç»™ operator

## 2.Strategies ç®¡ç†æµç¨‹æºç è§£æ

![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtsDSuacAA_7TL.jpg)

```
function addStrategiesToDepositWhitelist(
    IStrategy[] calldata strategiesToWhitelist,
    bool[] calldata thirdPartyTransfersForbiddenValues
) external onlyStrategyWhitelister {
    require(
        strategiesToWhitelist.length == thirdPartyTransfersForbiddenValues.length,
        "StrategyManager.addStrategiesToDepositWhitelist: array lengths do not match"
    );
    uint256 strategiesToWhitelistLength = strategiesToWhitelist.length;
    for (uint256 i = 0; i < strategiesToWhitelistLength; ) {
        // change storage and emit event only if strategy is not already in whitelist
        if (!strategyIsWhitelistedForDeposit[strategiesToWhitelist[i]]) {
            strategyIsWhitelistedForDeposit[strategiesToWhitelist[i]] = true;
            emit StrategyAddedToDepositWhitelist(strategiesToWhitelist[i]);
            _setThirdPartyTransfersForbidden(strategiesToWhitelist[i], thirdPartyTransfersForbiddenValues[i]);
        }
        unchecked {
            ++i;
        }
    }
}
```

- å‚æ•°æ˜¯ Strategy æ•°ç»„strategiesToWhitelistå’Œç¬¬ä¸‰æ–¹è½¬è´¦é™åˆ¶æ•°ç»„ thirdPartyTransfersForbiddenValues
- å°†ç­–ç•¥æ”¾åˆ° strategyIsWhitelistedForDeposit Map é‡Œé¢ï¼Œå¹¶å¼€å¯å¯è´¨æŠ¼æƒé™
- æŠ›å‡º StrategyAddedToDepositWhitelist äº‹ä»¶



```text
function removeStrategiesFromDepositWhitelist(
    IStrategy[] calldata strategiesToRemoveFromWhitelist
) external onlyStrategyWhitelister {
    uint256 strategiesToRemoveFromWhitelistLength = strategiesToRemoveFromWhitelist.length;
    for (uint256 i = 0; i < strategiesToRemoveFromWhitelistLength; ) {
        // change storage and emit event only if strategy is already in whitelist
        if (strategyIsWhitelistedForDeposit[strategiesToRemoveFromWhitelist[i]]) {
            strategyIsWhitelistedForDeposit[strategiesToRemoveFromWhitelist[i]] = false;
            emit StrategyRemovedFromDepositWhitelist(strategiesToRemoveFromWhitelist[i]);
            // Set mapping value to default false value
            _setThirdPartyTransfersForbidden(strategiesToRemoveFromWhitelist[i], false);
        }
        unchecked {
            ++i;
        }
    }
}
```

- å°†ç»‘å®šå…³ç³»çš„çš„ç­–ç•¥çš„ value ç½®æˆ false, è¿™æ ·è¿™ä¸ªç­–ç•¥å°±ä¸èƒ½å†æ¥æ”¶ staker çš„è´¨æŠ¼

## 3.è´¨æŠ¼æµç¨‹æºç è§£æ

**3.1.ETH è´¨æŠ¼**



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtsQGTawAArbzn.png)





```text
function stake(
    bytes calldata pubkey,
    bytes calldata signature,
    bytes32 depositDataRoot
) external payable onlyWhenNotPaused(PAUSED_NEW_EIGENPODS) {
    IEigenPod pod = ownerToPod[msg.sender];
    if (address(pod) == address(0)) {
        //deploy a pod if the sender doesn't have one already
        pod = _deployPod();
    }
    pod.stake{value: msg.value}(pubkey, signature, depositDataRoot);
}
```

- åˆ¤æ–­ pod æ˜¯å¦å·²ç»åˆ›å»ºï¼Œå¦‚æœæ²¡æœ‰åˆ›å»ºï¼Œä½¿ç”¨ create2 æ–¹å¼åˆ›å»º pod
- Pod åˆ›å»ºå®Œæˆä¹‹åï¼ŒæŠŠå¯¹åº”çš„ ETH è´¨æŠ¼åˆ° pod é‡Œé¢å»



```text
function stake(
    bytes calldata pubkey,
    bytes calldata signature,
    bytes32 depositDataRoot
) external payable onlyEigenPodManager {
    // stake on ethpos
    require(msg.value == 32 ether, "EigenPod.stake: must initially stake for any validator with 32 ether");
    ethPOS.deposit{value: 32 ether}(pubkey, _podWithdrawalCredentials(), signature, depositDataRoot);
    emit EigenPodStaked(pubkey);
}
```

- åˆ¤æ–­è´¨æŠ¼è€…æ˜¯å¦æœ‰ 32 ä¸ª ETHï¼Œå¦‚æœæœ‰çš„è¯ï¼Œå°† 32 ä¸ª ETH æ‰“å…¥åˆ°ä¿¡æ ‡é“¾åˆçº¦



```text
function verifyWithdrawalCredentials(
    uint64 beaconTimestamp,
    BeaconChainProofs.StateRootProof calldata stateRootProof,
    uint40[] calldata validatorIndices,
    bytes[] calldata validatorFieldsProofs,
    bytes32[][] calldata validatorFields
) external onlyOwnerOrProofSubmitter onlyWhenNotPaused(PAUSED_EIGENPODS_VERIFY_CREDENTIALS) {
    require(
        (validatorIndices.length == validatorFieldsProofs.length)
            && (validatorFieldsProofs.length == validatorFields.length),
        "EigenPod.verifyWithdrawalCredentials: validatorIndices and proofs must be same length"
    );

    // Calling this method using a `beaconTimestamp` <= `currentCheckpointTimestamp` would allow
    // a newly-verified validator to be submitted to `verifyCheckpointProofs`, making progress
    // on an existing checkpoint.
    require(
        beaconTimestamp > currentCheckpointTimestamp,
        "EigenPod.verifyWithdrawalCredentials: specified timestamp is too far in past"
    );

    // Verify passed-in `beaconStateRoot` against the beacon block root
    // forgefmt: disable-next-item
    BeaconChainProofs.verifyStateRoot({
        beaconBlockRoot: getParentBlockRoot(beaconTimestamp),
        proof: stateRootProof
    });

    uint256 totalAmountToBeRestakedWei;
    for (uint256 i = 0; i < validatorIndices.length; i++) {
        // forgefmt: disable-next-item
        totalAmountToBeRestakedWei += _verifyWithdrawalCredentials(
            stateRootProof.beaconStateRoot,
            validatorIndices[i],
            validatorFieldsProofs[i],
            validatorFields[i]
        );
    }

    eigenPodManager.recordBeaconChainETHBalanceUpdate(podOwner, int256(totalAmountToBeRestakedWei));
}
```

- éªŒè¯ä¿¡æ ‡é“¾çš„ stateRootProof
- éªŒè¯ WithdrawalCredentials å¹¶è¿”å›å¯¹åº”çš„èµ„é‡‘æ•°é‡
- è°ƒç”¨ recordBeaconChainETHBalanceUpdate æ–¹æ³•æ›´æ–°åˆçº¦ä¸­çš„ Balance ä¿¡æ¯ï¼ŒåŒæ—¶è°ƒåº¦ DelegationManager çš„æ–¹æ³•å®Œæˆè´¨æŠ¼ Share åˆ†é…å¯¹åº”è´¨æŠ¼è€…å§”æ‰˜ Operator



```text
function recordBeaconChainETHBalanceUpdate(
    address podOwner,
    int256 sharesDelta
) external onlyEigenPod(podOwner) nonReentrant {
    require(
        podOwner != address(0), "EigenPodManager.recordBeaconChainETHBalanceUpdate: podOwner cannot be zero address"
    );
    require(
        sharesDelta % int256(GWEI_TO_WEI) == 0,
        "EigenPodManager.recordBeaconChainETHBalanceUpdate: sharesDelta must be a whole Gwei amount"
    );
    int256 currentPodOwnerShares = podOwnerShares[podOwner];
    int256 updatedPodOwnerShares = currentPodOwnerShares + sharesDelta;
    podOwnerShares[podOwner] = updatedPodOwnerShares;

    // inform the DelegationManager of the change in delegateable shares
    int256 changeInDelegatableShares = _calculateChangeInDelegatableShares({
        sharesBefore: currentPodOwnerShares,
        sharesAfter: updatedPodOwnerShares
    });
    // skip making a call to the DelegationManager if there is no change in delegateable shares
    if (changeInDelegatableShares != 0) {
        if (changeInDelegatableShares < 0) {
            delegationManager.decreaseDelegatedShares({
                staker: podOwner,
                strategy: beaconChainETHStrategy,
                shares: uint256(-changeInDelegatableShares)
            });
        } else {
            delegationManager.increaseDelegatedShares({
                staker: podOwner,
                strategy: beaconChainETHStrategy,
                shares: uint256(changeInDelegatableShares)
            });
        }
    }
    emit PodSharesUpdated(podOwner, sharesDelta);
    emit NewTotalShares(podOwner, updatedPodOwnerShares);
}
```

- æ›´æ–°å¯¹åº”è´¨æŠ¼ shares
- è°ƒç”¨ DelegationManager çš„ decreaseDelegatedShares å’Œ increaseDelegatedShares å»æ›´æ–°è´¨æŠ¼è€…ç»‘å®š operator è´¨æŠ¼åˆ†é¢ã€‚

**3.2.ERC20**



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtsj7Jb0AEGPQr.png)





```text
function depositIntoStrategy(
    IStrategy strategy,
    IERC20 token,
    uint256 amount
) external onlyWhenNotPaused(PAUSED_DEPOSITS) nonReentrant returns (uint256 shares) {
    shares = _depositIntoStrategy(msg.sender, strategy, token, amount);
}
```

- è¯¥å‡½æ•°ç›´æ¥è°ƒåº¦ _depositIntoStrategy å®Œæˆ ERC20 Token è´¨æŠ¼è¿‡ç¨‹



```text
function depositIntoStrategyWithSignature(
    IStrategy strategy,
    IERC20 token,
    uint256 amount,
    address staker,
    uint256 expiry,
    bytes memory signature
) external onlyWhenNotPaused(PAUSED_DEPOSITS) nonReentrant returns (uint256 shares) {
    require(
        !thirdPartyTransfersForbidden[strategy],
        "StrategyManager.depositIntoStrategyWithSignature: third transfers disabled"
    );
    require(expiry >= block.timestamp, "StrategyManager.depositIntoStrategyWithSignature: signature expired");

    uint256 nonce = nonces[staker];
    bytes32 structHash = keccak256(abi.encode(DEPOSIT_TYPEHASH, staker, strategy, token, amount, nonce, expiry));
    unchecked {
        nonces[staker] = nonce + 1;
    }

  
    bytes32 digestHash = keccak256(abi.encodePacked("\x19\x01", domainSeparator(), structHash));

 
    EIP1271SignatureUtils.checkSignature_EIP1271(staker, digestHash, signature);

 
    shares = _depositIntoStrategy(staker, strategy, token, amount);
}
```

- éªŒè¯å®Œæˆç­¾åä¹‹åè°ƒç”¨ _depositIntoStrategy å®Œæˆ ERC20 Token è´¨æŠ¼æµç¨‹



```text
function _depositIntoStrategy(
    address staker,
    IStrategy strategy,
    IERC20 token,
    uint256 amount
) internal onlyStrategiesWhitelistedForDeposit(strategy) returns (uint256 shares) {

    token.safeTransferFrom(msg.sender, address(strategy), amount);

    shares = strategy.deposit(token, amount);

    _addShares(staker, token, strategy, shares);

    delegation.increaseDelegatedShares(staker, strategy, shares);

    return shares;
}
```

- ç¬¬ä¸€æ­¥ï¼šå°†éœ€è¦è´¨æŠ¼çš„ Token è½¬åˆ°å¯¹åº”ç­–ç•¥åˆçº¦
- ç¬¬äºŒæ­¥ï¼šè°ƒåº¦ strategy åˆçº¦çš„ deposit æ–¹æ³•ç®—å‡ºè´¨æŠ¼çš„ Shares
- ç¬¬ä¸‰æ­¥ï¼šå°†è´¨æŠ¼çš„ shares åŠ åˆ°å¯¹åº”è´¨æŠ¼è€…è´¨æŠ¼çš„ç­–ç•¥é‡Œé¢
- ç¬¬å››æ­¥ï¼šå°†è´¨æŠ¼è€…è´¨æŠ¼ shares åŠ ç»™è´¨æŠ¼è€…ç»‘å®š operator



```text
function deposit(
    IERC20 token,
    uint256 amount
) external virtual override onlyWhenNotPaused(PAUSED_DEPOSITS) onlyStrategyManager returns (uint256 newShares) {

    _beforeDeposit(token, amount);


    uint256 priorTotalShares = totalShares;

 
    uint256 virtualShareAmount = priorTotalShares + SHARES_OFFSET;
    uint256 virtualTokenBalance = _tokenBalance() + BALANCE_OFFSET;

    uint256 virtualPriorTokenBalance = virtualTokenBalance - amount;
    newShares = (amount * virtualShareAmount) / virtualPriorTokenBalance;


    require(newShares != 0, "StrategyBase.deposit: newShares cannot be zero");


    totalShares = (priorTotalShares + newShares);

    require(totalShares <= MAX_TOTAL_SHARES, "StrategyBase.deposit: totalShares exceeds `MAX_TOTAL_SHARES`");

   
    _emitExchangeRate(virtualTokenBalance, totalShares + SHARES_OFFSET);

    return newShares;
}
```

- è¯¥æ–¹æ³•ä½œç”¨æ˜¯æ ¹æ®å½“å‰è´¨æŠ¼æƒ…å†µå’Œè´¨æŠ¼è€…è´¨æŠ¼çš„èµ„é‡‘è®¡ç®—å‡ºå¯¹åº”è´¨æŠ¼ä»½é¢



```text
function _addShares(address staker, IERC20 token, IStrategy strategy, uint256 shares) internal {

    require(staker != address(0), "StrategyManager._addShares: staker cannot be zero address");
    require(shares != 0, "StrategyManager._addShares: shares should not be zero!");


    if (stakerStrategyShares[staker][strategy] == 0) {
        require(
            stakerStrategyList[staker].length < MAX_STAKER_STRATEGY_LIST_LENGTH,
            "StrategyManager._addShares: deposit would exceed MAX_STAKER_STRATEGY_LIST_LENGTH"
        );
        stakerStrategyList[staker].push(strategy);
    }

    stakerStrategyShares[staker][strategy] += shares;

    emit Deposit(staker, token, strategy, shares);
}
```

- stakerStrategyShares[staker][strategy] += shares è¯¥å‡½æ•°çš„æ ¸å¿ƒä»£ç ï¼Œå°†è´¨æŠ¼è€…è´¨æŠ¼åˆ°ç­–ç•¥çš„ä»½é¢åŠ ç»™è‡ªå·±



```text
function increaseDelegatedShares(
    address staker,
    IStrategy strategy,
    uint256 shares
) external onlyStrategyManagerOrEigenPodManager {
    if (isDelegated(staker)) {
        address operator = delegatedTo[staker];
        _increaseOperatorShares({operator: operator, staker: staker, strategy: strategy, shares: shares});
    }
}
```

- åˆ¤æ–­ stake æ˜¯å¦å·²ç» delegate ç»™è¯¥ operator, è‹¥å·²ç» delegate è¿‡äº†ï¼Œç›´æ¥è°ƒç”¨ _increaseOperatorShares å°†è´¨æŠ¼è€…çš„ shares åŠ ç»™ç»‘å®šçš„ operator



```text
function _increaseOperatorShares(address operator, address staker, IStrategy strategy, uint256 shares) internal {
    operatorShares[operator][strategy] += shares;
    emit OperatorSharesIncreased(operator, staker, strategy, shares);
}
```

- operatorShares[operator][strategy] += shares; è¯¥å‡½æ•°çš„æ ¸å¿ƒä»£ç ï¼Œå°†è´¨æŠ¼è€…çš„ shares åŠ ç»™ç»‘å®šçš„ operator å¯¹åº”ç­–ç•¥é‡Œé¢

## 4.Delegate æµç¨‹æºç è§£æ



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYts75gasAAQhT-.png)





```text
function delegateTo(
    address operator,
    SignatureWithExpiry memory approverSignatureAndExpiry,
    bytes32 approverSalt
) external {
    require(!isDelegated(msg.sender), "DelegationManager.delegateTo: staker is already actively delegated");
    require(isOperator(operator), "DelegationManager.delegateTo: operator is not registered in EigenLayer");

    _delegate(msg.sender, operator, approverSignatureAndExpiry, approverSalt);
}
```

- åˆ¤æ–­æ˜¯ä¸æ˜¯ Delegate è¿‡äº†
- åˆ¤æ–­ Operator æ˜¯ä¸æ˜¯æ³¨å†Œäº†
- è‹¥è¦ Delegate çš„ Operator å·²ç»æ³¨å†Œå¹¶ä¸”è¯¥ staker è¿˜æ²¡æœ‰ Delegate ç»™è¿‡å…¶ä»– Operator, è°ƒç”¨ _delegate æ–¹æ³•è¿›è¡Œå§”æ‰˜è´¨æŠ¼ä»½é¢ç»™ Operatort



```text
function _delegate(
    address staker,
    address operator,
    SignatureWithExpiry memory approverSignatureAndExpiry,
    bytes32 approverSalt
) internal onlyWhenNotPaused(PAUSED_NEW_DELEGATION) {

    address _delegationApprover = _operatorDetails[operator].delegationApprover;
  
    if (_delegationApprover != address(0) && msg.sender != _delegationApprover && msg.sender != operator) {
        // check the signature expiry
        require(
            approverSignatureAndExpiry.expiry >= block.timestamp,
            "DelegationManager._delegate: approver signature expired"
        );

        require(
            !delegationApproverSaltIsSpent[_delegationApprover][approverSalt],
            "DelegationManager._delegate: approverSalt already spent"
        );
        delegationApproverSaltIsSpent[_delegationApprover][approverSalt] = true;

        bytes32 approverDigestHash = calculateDelegationApprovalDigestHash(
            staker, 
            operator, 
            _delegationApprover, 
            approverSalt, 
            approverSignatureAndExpiry.expiry
        );


        EIP1271SignatureUtils.checkSignature_EIP1271(
            _delegationApprover, 
            approverDigestHash, 
            approverSignatureAndExpiry.signature
        );
    }


    delegatedTo[staker] = operator;
    emit StakerDelegated(staker, operator);

    (IStrategy[] memory strategies, uint256[] memory shares) = getDelegatableShares(staker);

    for (uint256 i = 0; i < strategies.length;) {
        // forgefmt: disable-next-item
        _increaseOperatorShares({
            operator: operator, 
            staker: staker, 
            strategy: strategies[i], 
            shares: shares[i]
        });

        unchecked {
            ++i;
        }
    }
}
```

- delegatedTo[staker] = operator: å°† Staker å’Œ Operator ç»‘å®š
- getDelegatableShares: è·å– staker å¯ä»¥å§”æ‰˜ç»™ Operator çš„ä»½é¢
- _increaseOperatorShares: å°† staker çš„è´¨æŠ¼ä»½é¢åŠ ç»™ç»‘å®š Operator

## 5.æ’é˜Ÿæµç¨‹å’Œæºç è§£æ



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYttHqLbsAABLdg.png)





```text
function undelegate(address staker)
    external
    onlyWhenNotPaused(PAUSED_ENTER_WITHDRAWAL_QUEUE)
    returns (bytes32[] memory withdrawalRoots)
{
    require(isDelegated(staker), "DelegationManager.undelegate: staker must be delegated to undelegate");
    require(!isOperator(staker), "DelegationManager.undelegate: operators cannot be undelegated");
    require(staker != address(0), "DelegationManager.undelegate: cannot undelegate zero address");
    address operator = delegatedTo[staker];
    require(
        msg.sender == staker || msg.sender == operator
            || msg.sender == _operatorDetails[operator].delegationApprover,
        "DelegationManager.undelegate: caller cannot undelegate staker"
    );

    (IStrategy[] memory strategies, uint256[] memory shares) = getDelegatableShares(staker);


    if (msg.sender != staker) {
        emit StakerForceUndelegated(staker, operator);
    }


    emit StakerUndelegated(staker, operator);
    delegatedTo[staker] = address(0);


    if (strategies.length == 0) {
        withdrawalRoots = new bytes32[](0);
    } else {
        withdrawalRoots = new bytes32[](strategies.length);
        for (uint256 i = 0; i < strategies.length; i++) {
            IStrategy[] memory singleStrategy = new IStrategy[](1);
            uint256[] memory singleShare = new uint256[](1);
            singleStrategy[0] = strategies[i];
            singleShare[0] = shares[i];

            withdrawalRoots[i] = _removeSharesAndQueueWithdrawal({
                staker: staker,
                operator: operator,
                withdrawer: staker,
                strategies: singleStrategy,
                shares: singleShare
            });
        }
    }
    return withdrawalRoots;
}
```

- getDelegatableShares è·å– staker åœ¨æ‰€æœ‰ç­–ç•¥é‡Œé¢å¯ä»¥åˆ é™¤çš„è´¨æŠ¼ä»½é¢
- delegatedTo[staker] = address(0)ï¼šè§£é™¤ Staker å’Œ Operator çš„ç»‘å®šå…³ç³»
- _removeSharesAndQueueWithdrawal:ç§»é™¤å¯¹åº”è´¨æŠ¼è€…è´¨æŠ¼ä»½é¢å’Œå§”æ‰˜ç»™ Operator ä»½é¢ï¼Œç”Ÿæˆä¸€ç¬”å¯¹åº” shares çš„æ’é˜Ÿå–æ¬¾çš„äº¤æ˜“



```text
function queueWithdrawals(QueuedWithdrawalParams[] calldata queuedWithdrawalParams)
    external
    onlyWhenNotPaused(PAUSED_ENTER_WITHDRAWAL_QUEUE)
    returns (bytes32[] memory)
{
    bytes32[] memory withdrawalRoots = new bytes32[](queuedWithdrawalParams.length);
    address operator = delegatedTo[msg.sender];

    for (uint256 i = 0; i < queuedWithdrawalParams.length; i++) {
        require(
            queuedWithdrawalParams[i].strategies.length == queuedWithdrawalParams[i].shares.length,
            "DelegationManager.queueWithdrawal: input length mismatch"
        );
        require(
            queuedWithdrawalParams[i].withdrawer == msg.sender,
            "DelegationManager.queueWithdrawal: withdrawer must be staker"
        );

        withdrawalRoots[i] = _removeSharesAndQueueWithdrawal({
            staker: msg.sender,
            operator: operator,
            withdrawer: queuedWithdrawalParams[i].withdrawer,
            strategies: queuedWithdrawalParams[i].strategies,
            shares: queuedWithdrawalParams[i].shares
        });
    }
    return withdrawalRoots;
}
```

- _removeSharesAndQueueWithdrawal:ç§»é™¤å¯¹åº”è´¨æŠ¼è€…è´¨æŠ¼ä»½é¢å’Œå§”æ‰˜ç»™ Operator ä»½é¢ï¼Œç”Ÿæˆä¸€ç¬”å¯¹åº” shares çš„æ’é˜Ÿå–æ¬¾çš„äº¤æ˜“



```text
function _removeSharesAndQueueWithdrawal(
    address staker,
    address operator,
    address withdrawer,
    IStrategy[] memory strategies,
    uint256[] memory shares
) internal returns (bytes32) {
    require(
        staker != address(0), "DelegationManager._removeSharesAndQueueWithdrawal: staker cannot be zero address"
    );
    require(strategies.length != 0, "DelegationManager._removeSharesAndQueueWithdrawal: strategies cannot be empty");

    for (uint256 i = 0; i < strategies.length;) {
       
        if (operator != address(0)) {

            _decreaseOperatorShares({
                operator: operator, 
                staker: staker, 
                strategy: strategies[i], 
                shares: shares[i]
            });
        }

        if (strategies[i] == beaconChainETHStrategy) {
            eigenPodManager.removeShares(staker, shares[i]);
        } else {
            require(
                staker == withdrawer || !strategyManager.thirdPartyTransfersForbidden(strategies[i]),
                "DelegationManager._removeSharesAndQueueWithdrawal: withdrawer must be same address as staker if thirdPartyTransfersForbidden are set"
            );

            strategyManager.removeShares(staker, strategies[i], shares[i]);
        }

        unchecked {
            ++i;
        }
    }

    // Create queue entry and increment withdrawal nonce
    uint256 nonce = cumulativeWithdrawalsQueued[staker];
    cumulativeWithdrawalsQueued[staker]++;

    Withdrawal memory withdrawal = Withdrawal({
        staker: staker,
        delegatedTo: operator,
        withdrawer: withdrawer,
        nonce: nonce,
        startBlock: uint32(block.number),
        strategies: strategies,
        shares: shares
    });

    bytes32 withdrawalRoot = calculateWithdrawalRoot(withdrawal);

    pendingWithdrawals[withdrawalRoot] = true;

    emit WithdrawalQueued(withdrawalRoot, withdrawal);
    return withdrawalRoot;
}
```

- è°ƒç”¨ _decreaseOperatorShares æŠŠ staker å§”æ‰˜ç»™ operator çš„è´¨æŠ¼ä»½é¢ç§»é™¤
- æŠŠ staker åœ¨ EigenPodManager å’Œ strategyManager é‡Œé¢çš„è´¨æŠ¼ shares ç§»é™¤æ‰
- æ ¹æ®ç§»é™¤çš„ shares ç”Ÿæˆä¸€ç¬”æ’é˜Ÿå–æ¬¾çš„äº¤æ˜“

## 6.å®Œæˆå–æ¬¾æµç¨‹å’Œæºç è§£æ



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYttdJEagAAx7-H.png)





```text
function completeQueuedWithdrawals(
    Withdrawal[] calldata withdrawals,
    IERC20[][] calldata tokens,
    uint256[] calldata middlewareTimesIndexes,
    bool[] calldata receiveAsTokens
) external onlyWhenNotPaused(PAUSED_EXIT_WITHDRAWAL_QUEUE) nonReentrant {
    for (uint256 i = 0; i < withdrawals.length; ++i) {
        _completeQueuedWithdrawal(withdrawals[i], tokens[i], middlewareTimesIndexes[i], receiveAsTokens[i]);
    }
}
```

ä¼ å…¥è¦æç°çš„äº¤æ˜“ä¿¡æ¯ï¼Œtoken å’Œ middlewareTimesIndexes å’Œæ˜¯å¦æ¥å— Tokens å‚æ•°ï¼Œè°ƒç”¨ _completeQueuedWithdrawal éå†æ‰€éœ€è¦æç°çš„äº¤æ˜“ï¼Œå¹¶å¤„ç†æ¯ä¸€ç¬”æ’é˜Ÿå–æ¬¾çš„äº¤æ˜“



```text
function _completeQueuedWithdrawal(
    Withdrawal calldata withdrawal,
    IERC20[] calldata tokens,
    uint256, /*middlewareTimesIndex*/
    bool receiveAsTokens
) internal {
    bytes32 withdrawalRoot = calculateWithdrawalRoot(withdrawal);

    require(
        pendingWithdrawals[withdrawalRoot], "DelegationManager._completeQueuedWithdrawal: action is not in queue"
    );

    require(
        withdrawal.startBlock + minWithdrawalDelayBlocks <= block.number,
        "DelegationManager._completeQueuedWithdrawal: minWithdrawalDelayBlocks period has not yet passed"
    );

    require(
        msg.sender == withdrawal.withdrawer,
        "DelegationManager._completeQueuedWithdrawal: only withdrawer can complete action"
    );

    if (receiveAsTokens) {
        require(
            tokens.length == withdrawal.strategies.length,
            "DelegationManager._completeQueuedWithdrawal: input length mismatch"
        );
    }
  
    delete pendingWithdrawals[withdrawalRoot];

    if (receiveAsTokens) {


        for (uint256 i = 0; i < withdrawal.strategies.length;) {
            require(
                withdrawal.startBlock + strategyWithdrawalDelayBlocks[withdrawal.strategies[i]] <= block.number,
                "DelegationManager._completeQueuedWithdrawal: withdrawalDelayBlocks period has not yet passed for this strategy"
            );

            _withdrawSharesAsTokens({
                staker: withdrawal.staker,
                withdrawer: msg.sender,
                strategy: withdrawal.strategies[i],
                shares: withdrawal.shares[i],
                token: tokens[i]
            });
            unchecked {
                ++i;
            }
        }
    } else {
        address currentOperator = delegatedTo[msg.sender];
        for (uint256 i = 0; i < withdrawal.strategies.length;) {
            require(
                withdrawal.startBlock + strategyWithdrawalDelayBlocks[withdrawal.strategies[i]] <= block.number,
                "DelegationManager._completeQueuedWithdrawal: withdrawalDelayBlocks period has not yet passed for this strategy"
            );

            if (withdrawal.strategies[i] == beaconChainETHStrategy) {
                address staker = withdrawal.staker;
                uint256 increaseInDelegateableShares =
                    eigenPodManager.addShares({podOwner: staker, shares: withdrawal.shares[i]});
                address podOwnerOperator = delegatedTo[staker];

                if (podOwnerOperator != address(0)) {
                    _increaseOperatorShares({
                        operator: podOwnerOperator,
                        staker: staker,
                        strategy: withdrawal.strategies[i],
                        shares: increaseInDelegateableShares
                    });
                }
            } else {
                strategyManager.addShares(msg.sender, tokens[i], withdrawal.strategies[i], withdrawal.shares[i]);
                if (currentOperator != address(0)) {
                    _increaseOperatorShares({
                        operator: currentOperator,
                        // the 'staker' here is the address receiving new shares
                        staker: msg.sender,
                        strategy: withdrawal.strategies[i],
                        shares: withdrawal.shares[i]
                    });
                }
            }
            unchecked {
                ++i;
            }
        }
    }
    emit WithdrawalCompleted(withdrawalRoot);
}
```

- delete pendingWithdrawals[withdrawalRoot] åˆ é™¤æ’é˜Ÿå–æ¬¾çš„äº¤æ˜“
- å¦‚æœ receiveAsTokens ä¸º true, æ‰§è¡Œ _withdrawSharesAsTokens å–èµ„é‡‘ï¼Œ_withdrawSharesAsTokens æ–¹ä¾¿ä¼šå»è°ƒç”¨ strategyManager.withdrawSharesAsTokens æˆ–è€… eigenPodManager.withdrawSharesAsTokens æ ¹æ®è´¨æŠ¼çš„ shares è®¡ç®—å‡ºè¦æç°çš„èµ„é‡‘ï¼Œå°†èµ„é‡‘è½¬åˆ°ç”¨æˆ·çš„æç°åœ°å€
- å¦‚æœ receiveAsTokens ä¸º false,  å°†è¦ç§»é™¤çš„ shares å†ç»™ staker å’Œ operator åŠ å›å»ã€‚

## 7.ä¿¡æ ‡é“¾ Validator é€€å‡ºæµç¨‹å’Œæºç è§£æ



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYttp2sbEAAbT3d.png)





```text
function startCheckpoint(bool revertIfNoBalance)
    external
    onlyOwnerOrProofSubmitter
    onlyWhenNotPaused(PAUSED_START_CHECKPOINT)
{
    _startCheckpoint(revertIfNoBalance);
}
```

- åˆ›å»ºä¸€ä¸ªæ£€æŸ¥ç‚¹ï¼Œç”¨äºè¯æ˜æ­¤ pod çš„æ´»åŠ¨éªŒè¯å™¨é›†ã€‚é€šè¿‡ä¸ºæ¯ä¸ªæ´»åŠ¨éªŒè¯å™¨æäº¤ä¸€ä¸ªæ£€æŸ¥ç‚¹è¯æ˜æ¥å®Œæˆæ£€æŸ¥ç‚¹ã€‚åœ¨æ£€æŸ¥ç‚¹è¿‡ç¨‹ä¸­ï¼Œå°†è·Ÿè¸ªæ´»åŠ¨éªŒè¯å™¨ä½™é¢çš„æ€»å˜åŒ–ï¼Œå¹¶å°†ä»»ä½•ä½™é¢ä¸º 0 çš„éªŒè¯å™¨æ ‡è®°ä¸ºâ€œWITHDRAWNâ€ã€‚
- ä¸€æ—¦å®Œæˆï¼Œpod æ‰€æœ‰è€…å°†è·å¾—ä¸ä»¥ä¸‹å†…å®¹ç›¸å¯¹åº”çš„ä»½é¢ï¼š å…¶æ´»åŠ¨éªŒè¯å™¨ä½™é¢çš„æ€»å˜åŒ– pod ä¸­å°šæœªæˆäºˆä»½é¢çš„ä»»ä½• ETH
- å¦‚æœ pod å·²ç»æœ‰ä¸€ä¸ªæœªå®Œæˆçš„æ£€æŸ¥ç‚¹ï¼Œåˆ™æ— æ³•åˆ›å»ºæ£€æŸ¥ç‚¹ã€‚å¦‚æœæ˜¯è¿™ç§æƒ…å†µï¼Œpod æ‰€æœ‰è€…å¿…é¡»åœ¨å¼€å§‹æ–°çš„æ£€æŸ¥ç‚¹ä¹‹å‰å®Œæˆç°æœ‰æ£€æŸ¥ç‚¹ã€‚
- å¦‚æœ pod ETH ä½™é¢ä¸º 0ï¼Œåˆ™å¼ºåˆ¶æ¢å¤ã€‚è¿™å…è®¸ pod æ‰€æœ‰è€…é˜²æ­¢æ„å¤–å¯åŠ¨ä¸ä¼šå¢åŠ å…¶ä»½é¢çš„æ£€æŸ¥ç‚¹



```text
function _startCheckpoint(bool revertIfNoBalance) internal {
    require(
        currentCheckpointTimestamp == 0,
        "EigenPod._startCheckpoint: must finish previous checkpoint before starting another"
    );
    
    require(
        lastCheckpointTimestamp != uint64(block.timestamp),
        "EigenPod._startCheckpoint: cannot checkpoint twice in one block"
    );

    uint64 podBalanceGwei = uint64(address(this).balance / GWEI_TO_WEI) - withdrawableRestakedExecutionLayerGwei;


    if (revertIfNoBalance && podBalanceGwei == 0) {
        revert("EigenPod._startCheckpoint: no balance available to checkpoint");
    }

    Checkpoint memory checkpoint = Checkpoint({
        beaconBlockRoot: getParentBlockRoot(uint64(block.timestamp)),
        proofsRemaining: uint24(activeValidatorCount),
        podBalanceGwei: podBalanceGwei,
        balanceDeltasGwei: 0
    });

    currentCheckpointTimestamp = uint64(block.timestamp);
    _updateCheckpoint(checkpoint);

    emit CheckpointCreated(uint64(block.timestamp), checkpoint.beaconBlockRoot, checkpoint.proofsRemaining);
}
```

- é€šè¿‡å¿«ç…§ pod çš„ ETH ä½™é¢å’Œå½“å‰å—çš„çˆ¶å—æ ¹æ¥å¯åŠ¨æ£€æŸ¥ç‚¹è¯æ˜ã€‚åœ¨ä¸ºpod çš„æ¯ä¸ª ACTIVE éªŒè¯å™¨æä¾›æ£€æŸ¥ç‚¹è¯æ˜åï¼Œpod çš„ ETH ä½™é¢å°†è·å¾—ä»½é¢å¹¶å¯ä»¥æå–
- éªŒè¯å™¨æ˜¯å…·æœ‰ç»è¿‡éªŒè¯çš„æå–å‡­æ®çš„éªŒè¯å™¨ï¼ˆæœ‰å…³è¯¦ç»†ä¿¡æ¯ï¼Œè¯·å‚é˜…verifyWithdrawalCredentialsï¼‰
- å¦‚æœ pod æ²¡æœ‰ä»»ä½• ACTIVE éªŒè¯å™¨ï¼Œåˆ™æ£€æŸ¥ç‚¹å°†è‡ªåŠ¨å®Œæˆ
- ä¸€æ—¦å¯åŠ¨ï¼Œæ£€æŸ¥ç‚¹å¿…é¡»å®Œæˆï¼å¦‚æœç°æœ‰æ£€æŸ¥ç‚¹ä¸å®Œæ•´ï¼Œåˆ™æ— æ³•å¯åŠ¨æ£€æŸ¥ç‚¹
- å¦‚æœå¯ç”¨äºæ£€æŸ¥ç‚¹çš„ ETH ä½™é¢ä¸º 0 ä¸”æ­¤å€¼ä¸ºtrueï¼Œåˆ™æ­¤æ–¹æ³•å°†æ¢å¤



```text
function _updateCheckpoint(Checkpoint memory checkpoint) internal {
    if (checkpoint.proofsRemaining == 0) {
        int256 totalShareDeltaWei =
            (int128(uint128(checkpoint.podBalanceGwei)) + checkpoint.balanceDeltasGwei) * int256(GWEI_TO_WEI);

        // Add any native ETH in the pod to `withdrawableRestakedExecutionLayerGwei`
        // ... this amount can be withdrawn via the `DelegationManager` withdrawal queue
        withdrawableRestakedExecutionLayerGwei += checkpoint.podBalanceGwei;

        // Finalize the checkpoint
        lastCheckpointTimestamp = currentCheckpointTimestamp;
        delete currentCheckpointTimestamp;
        delete _currentCheckpoint;

        eigenPodManager.recordBeaconChainETHBalanceUpdate(podOwner, totalShareDeltaWei);
        emit CheckpointFinalized(lastCheckpointTimestamp, totalShareDeltaWei);
    } else {
        _currentCheckpoint = checkpoint;
    }
}
```

- å®Œæˆæ£€æŸ¥ç‚¹çš„è¿›åº¦å¹¶å°†å…¶å­˜å‚¨åœ¨çŠ¶æ€ä¸­ã€‚
- å¦‚æœæ£€æŸ¥ç‚¹æ²¡æœ‰å‰©ä½™çš„è¯æ˜ï¼Œåˆ™æœ€ç»ˆç¡®å®šï¼š è®¡ç®—å…±äº«å¢é‡å¹¶å‘é€åˆ° EigenPodManager å°†æ£€æŸ¥ç‚¹çš„ podBalanceGwei æ·»åŠ åˆ° withdrawableRestakedExecutionLayerGwei æ›´æ–° lastCheckpointTimestamp åˆ é™¤ _currentCheckpoint å’Œ currentCheckpointTimestamp
- è°ƒç”¨ recordBeaconChainETHBalanceUpdate æ›´æ–° DelegationManager é‡Œé¢çš„è´¨æŠ¼ä»½é¢



![](https://github.com/the-web3/eigenlayer-eigenda-tech/blob/main/eigenlayer/imgs/GYtud0qbkAAaUit.png)





```text
function verifyCheckpointProofs(
    BeaconChainProofs.BalanceContainerProof calldata balanceContainerProof,
    BeaconChainProofs.BalanceProof[] calldata proofs
) external onlyWhenNotPaused(PAUSED_EIGENPODS_VERIFY_CHECKPOINT_PROOFS) {
    uint64 checkpointTimestamp = currentCheckpointTimestamp;
    require(
        checkpointTimestamp != 0,
        "EigenPod.verifyCheckpointProofs: must have active checkpoint to perform checkpoint proof"
    );

    Checkpoint memory checkpoint = _currentCheckpoint;

    BeaconChainProofs.verifyBalanceContainer({
        beaconBlockRoot: checkpoint.beaconBlockRoot,
        proof: balanceContainerProof
    });

    uint64 exitedBalancesGwei;
    for (uint256 i = 0; i < proofs.length; i++) {
        BeaconChainProofs.BalanceProof calldata proof = proofs[i];
        ValidatorInfo memory validatorInfo = _validatorPubkeyHashToInfo[proof.pubkeyHash];

        if (validatorInfo.status != VALIDATOR_STATUS.ACTIVE) {
            continue;
        }

    
        if (validatorInfo.lastCheckpointedAt >= checkpointTimestamp) {
            continue;
        }
        
        (int128 balanceDeltaGwei, uint64 exitedBalanceGwei) = _verifyCheckpointProof({
            validatorInfo: validatorInfo,
            checkpointTimestamp: checkpointTimestamp,
            balanceContainerRoot: balanceContainerProof.balanceContainerRoot,
            proof: proof
        });

        checkpoint.proofsRemaining--;
        checkpoint.balanceDeltasGwei += balanceDeltaGwei;
        exitedBalancesGwei += exitedBalanceGwei;


        _validatorPubkeyHashToInfo[proof.pubkeyHash] = validatorInfo;
        emit ValidatorCheckpointed(checkpointTimestamp, uint40(validatorInfo.validatorIndex));
    }

    checkpointBalanceExitedGwei[checkpointTimestamp] += exitedBalancesGwei;
    _updateCheckpoint(checkpoint);
}
```

- é€šè¿‡æäº¤ä¸€ä¸ªæˆ–å¤šä¸ªéªŒè¯å™¨æ¥æ¨è¿›å½“å‰æ£€æŸ¥ç‚¹çš„å®Œæˆ
- æ£€æŸ¥ç‚¹è¯æ˜ã€‚ä»»ä½•äººéƒ½å¯ä»¥è°ƒç”¨æ­¤æ–¹æ³•æäº¤å½“å‰æ£€æŸ¥ç‚¹çš„è¯æ˜ã€‚
- å¯¹äºæ¯ä¸ªå·²è¯æ˜çš„éªŒè¯å™¨ï¼Œå½“å‰æ£€æŸ¥ç‚¹çš„ proofsRemaining éƒ½ä¼šå‡å°‘ã€‚
- å¦‚æœæ£€æŸ¥ç‚¹çš„ proofsRemaining è¾¾åˆ° 0ï¼Œåˆ™æ£€æŸ¥ç‚¹å®Œæˆã€‚ï¼ˆæœ‰å…³æ›´å¤šè¯¦ç»†ä¿¡æ¯ï¼Œè¯·å‚é˜… _updateCheckpointï¼‰ä»…å½“å­˜åœ¨å½“å‰æ´»åŠ¨çš„æ£€æŸ¥ç‚¹æ—¶æ‰èƒ½è°ƒç”¨æ­¤æ–¹æ³•ã€‚
- balanceContainerProof æ ¹æ®æ£€æŸ¥ç‚¹çš„ beaconBlockRoot è¯æ˜ä¿¡æ ‡çš„å½“å‰ä½™é¢å®¹å™¨æ ¹
- proofs æ ¹æ® balanceContainerRoot ä¸ºä¸€ä¸ªæˆ–å¤šä¸ªéªŒè¯å™¨å½“å‰ä½™é¢æä¾›è¯æ˜
- è°ƒåº¦åˆ° _updateCheckpoint å’Œä¸Šé¢çš„æµç¨‹ä¸€æ ·

# å››.æ€»ç»“

EigenLayer æ˜¯ä¸€ä¸ªåˆ›æ–°çš„ **ä»¥å¤ªåŠé‡è´¨æŠ¼ï¼ˆrestakingï¼‰** åè®®ï¼Œå…è®¸ç”¨æˆ·å°†ä»–ä»¬åœ¨ä»¥å¤ªåŠä¸»ç½‘ä¸Šçš„è´¨æŠ¼èµ„äº§ï¼ˆå¦‚ ETHï¼‰å†æ¬¡è´¨æŠ¼ï¼Œä»¥ç”¨äºæ–°çš„æœåŠ¡ã€‚è¿™äº›æœåŠ¡å¯èƒ½åŒ…æ‹¬æ•°æ®å¯ç”¨æ€§ã€å…±è¯†æœºåˆ¶ã€æˆ–å…¶ä»–é“¾å¤–ä»»åŠ¡ã€‚EigenLayer çš„æ ¸å¿ƒç†å¿µæ˜¯å……åˆ†åˆ©ç”¨ä»¥å¤ªåŠè´¨æŠ¼è€…çš„å®‰å…¨æ€§å’Œå»ä¸­å¿ƒåŒ–ï¼ŒåŒæ—¶ä¸ºæ–°çš„åŒºå—é“¾æœåŠ¡å’Œé¡¹ç›®æä¾›é¢å¤–çš„å®‰å…¨ä¿éšœã€‚

## æ ¸å¿ƒæœºåˆ¶

- **é‡è´¨æŠ¼ï¼ˆRestakingï¼‰**ï¼šEigenLayer çš„æ ¸å¿ƒåŠŸèƒ½æ˜¯å…è®¸è´¨æŠ¼è€…å°†ä»–ä»¬å·²ç»è´¨æŠ¼åœ¨ä»¥å¤ªåŠä¸Šçš„èµ„äº§å†è´¨æŠ¼åˆ° EigenLayer æä¾›çš„å…¶ä»–æœåŠ¡ä¸­ã€‚è¿™ä½¿å¾—è´¨æŠ¼è€…èƒ½å¤Ÿä¸ºå¤šä¸ªé¡¹ç›®æä¾›æ”¯æŒï¼ŒåŒæ—¶åªä½¿ç”¨ä»–ä»¬çš„è´¨æŠ¼èµ„äº§ã€‚
- **å¯æ‰©å±•çš„éªŒè¯äººç½‘ç»œ**ï¼šEigenLayer æ‹“å±•äº†ç°æœ‰çš„ä»¥å¤ªåŠéªŒè¯äººç½‘ç»œï¼Œå…è®¸è¿™äº›éªŒè¯äººä¸åªä¸ºä»¥å¤ªåŠåŒºå—é“¾æœåŠ¡ï¼Œè¿˜å¯ä»¥ä¸ºå…¶ä»– L2 ç½‘ç»œã€è·¨é“¾æ¡¥ã€æˆ–å»ä¸­å¿ƒåŒ–åº”ç”¨æä¾›å®‰å…¨æœåŠ¡ã€‚
- **æ¨¡å—åŒ–æœåŠ¡**ï¼šEigenLayer æä¾›ä¸€ä¸ªå¹³å°ï¼Œå…è®¸å¼€å‘è€…åˆ›å»ºè‡ªå·±çš„å»ä¸­å¿ƒåŒ–æœåŠ¡ï¼Œå¹¶å°†å…¶æ¥å…¥ EigenLayer çš„éªŒè¯äººç½‘ç»œã€‚è¿™äº›æœåŠ¡å¯èƒ½åŒ…æ‹¬æ•°æ®å¯ç”¨æ€§è¯æ˜ã€ä¾§é“¾éªŒè¯ç­‰ã€‚
- **æƒ©ç½šæœºåˆ¶ï¼ˆSlashingï¼‰**ï¼šEigenLayer ä¸­çš„è´¨æŠ¼è€…å°†é¢ä¸´åŒé‡æƒ©ç½šé£é™©ã€‚å¦‚æœä»–ä»¬åœ¨æ‰§è¡Œä»»åŠ¡æ—¶è¡¨ç°ä¸ä½³ï¼ˆä¾‹å¦‚éªŒè¯é”™è¯¯çš„æ•°æ®ï¼‰ï¼Œä»–ä»¬ä¸ä»…ä¼šåœ¨åŸå§‹è´¨æŠ¼çš„ä»¥å¤ªåŠä¸­å—åˆ°æƒ©ç½šï¼Œè¿˜å¯èƒ½åœ¨ EigenLayer ä¸­é­é‡èµ„äº§å‰Šå‡ã€‚

## å…³é”®ä¼˜åŠ¿

- **èµ„æºé«˜æ•ˆåˆ©ç”¨**ï¼šEigenLayer é€šè¿‡å…è®¸é‡è´¨æŠ¼ï¼Œä½¿ä»¥å¤ªåŠè´¨æŠ¼è€…èƒ½å¤Ÿå¤šæ¬¡åˆ©ç”¨åŒä¸€ä»½è´¨æŠ¼èµ„äº§ï¼Œè€Œä¸å¿…ä¸ºæ¯ä¸ªæ–°æœåŠ¡å•ç‹¬è´¨æŠ¼ï¼Œä»è€Œæé«˜äº†èµ„æœ¬æ•ˆç‡ã€‚
- **å¢å¼ºå»ä¸­å¿ƒåŒ–å’Œå®‰å…¨æ€§**ï¼šEigenLayer åˆ©ç”¨äº†ä»¥å¤ªåŠç°æœ‰çš„å»ä¸­å¿ƒåŒ–ç½‘ç»œå’Œç»æµæ¿€åŠ±æœºåˆ¶ï¼Œå¸®åŠ©æ–°çš„é¡¹ç›®å®ç°å»ä¸­å¿ƒåŒ–å’Œå®‰å…¨æ€§ï¼Œè€Œæ— éœ€è‡ªå·±ä»å¤´æ­å»ºéªŒè¯äººç½‘ç»œã€‚
- **åˆ›æ–°ç”Ÿæ€**ï¼šEigenLayer ä¸ºå¼€å‘è€…æä¾›äº†ä¸€ä¸ªåŸºç¡€è®¾æ–½ï¼Œå¯ä»¥æ„å»ºå„ç§æ–°çš„å»ä¸­å¿ƒåŒ–æœåŠ¡ï¼Œå¹¶è½»æ¾æ¥å…¥ä»¥å¤ªåŠè´¨æŠ¼è€…ç½‘ç»œã€‚è¿™ä¸ºå»ä¸­å¿ƒåŒ–æœåŠ¡çš„å¿«é€Ÿæ‰©å±•å’Œåˆ›æ–°æä¾›äº†ä¸€ä¸ªå¼ºå¤§çš„å¹³å°ã€‚

## å‘å±•å‰æ™¯

EigenLayer é€šè¿‡å°†ç°æœ‰çš„ä»¥å¤ªåŠè´¨æŠ¼ç½‘ç»œæ‰©å±•è‡³æ–°çš„æœåŠ¡é¢†åŸŸï¼Œæå‡äº†åŒºå—é“¾æŠ€æœ¯çš„ç»æµå’Œå®‰å…¨æ€§ï¼ŒåŒæ—¶ä¸ºå¼€å‘è€…æä¾›äº†çµæ´»çš„åŸºç¡€è®¾æ–½ã€‚å®ƒæœ‰æ½œåŠ›åœ¨å»ä¸­å¿ƒåŒ–æ•°æ®å­˜å‚¨ã€è·¨é“¾æ¡¥æ¥ã€éªŒè¯ä»»åŠ¡ç­‰é¢†åŸŸæ¨åŠ¨æ–°çš„åˆ›æ–°ï¼Œè¿›ä¸€æ­¥å¢å¼ºä»¥å¤ªåŠç”Ÿæ€ç³»ç»Ÿçš„ä»·å€¼ã€‚